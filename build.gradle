plugins {
  id 'eclipse'
  id "com.moowork.node" version "0.11"
}

ext {
    libDir = "${project.buildDir}/lib/"
    jsxDir = 'src/jsx'
    htmlDir = 'src/html'
    cssDir = 'src/css'
    appBuildDir = "$buildDir/app"
    appBuildJsDir = "$appBuildDir/js"
    jsBundleFile = "$buildDir/bundle.js"
    cssBundleDir = "$appBuildDir/css"
}

task compileJsx(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.dir jsxDir
    outputs.dir libDir

    script = file('node_modules/babel-cli/bin/babel')
    args = [jsxDir, '--out-dir', libDir]
}

task test(type: NpmTask, dependsOn: 'compileJsx') {
    inputs.file '__tests__'

    args = ['test']
}

task bundleJs(type: NodeTask, dependsOn: 'compileJsx') {
    inputs.dir libDir
    outputs.file jsBundleFile

    script = file('node_modules/browserify/bin/cmd')
    args = ["$libDir/app.js", '-o', jsBundleFile]
}

task bowerInstall(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.file 'bower.json'
    outputs.dir cssDir

    script = file('node_modules/bower-installer/bower-installer')
    args = ['install']
}

task compileCss(type: NodeTask, dependsOn: 'bowerInstall') {
    inputs.dir cssDir
    outputs.dir cssBundleDir

    script = file('node_modules/node-sass/bin/node-sass')
    args = ["$cssDir/main.scss", '-o', cssBundleDir]
}

task copyHtml(type: Copy) {
    inputs.dir htmlDir
    outputs.dir appBuildDir

    from htmlDir
    into appBuildDir
}

task copyJs(type: Copy, dependsOn: 'bundleJs') {
    inputs.file jsBundleFile
    outputs.dir appBuildJsDir

    from jsBundleFile
    into appBuildJsDir
}

//task build(type: Copy, dependsOn: ['test', 'copyHtml', 'copyJs', 'compileCss'])
task build(type: Copy, dependsOn: ['copyHtml', 'copyJs', 'compileCss'])

task clean(type: Delete) {
    delete project.buildDir
}
