plugins {
  id 'eclipse'
  id "com.moowork.node" version "0.11"
}

node {
  version = '4.1.1'
  npmVersion = '2.1.5'
  download = true
}

ext {
    libDir = "${project.buildDir}/lib/"
    jsxDir = 'src/jsx'
    htmlDir = 'src/html'
}

task compileJsx(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.file jsxDir
    script = file('node_modules/babel/bin/babel')
    args = [jsxDir, '--out-dir', libDir]
}

task test(type: NpmTask, dependsOn: 'compileJsx') {
    inputs.file '__tests__'
    args = ['test']
}

task bundleJs(type: NodeTask, dependsOn: 'compileJsx') {
    inputs.file libDir
    script = file('node_modules/browserify/bin/cmd')
    args = ['-r', 'react', "$libDir/app.js", '-o', "$buildDir/bundle.js"]
}

task copyHtml(type: Copy) {
    inputs.file htmlDir
    from 'src/html'
    into "$buildDir/app"
}

task copyJs(type: Copy, dependsOn: 'bundleJs') {
    from "$buildDir/bundle.js"
    into "$buildDir/app/js"
}

task build(type: Copy, dependsOn: ['test', 'copyHtml', 'copyJs'])

task clean(type: Delete) {
    delete project.buildDir
}
